group 'com.github.pscheidl'
version '0.2.2'

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:1.0.1.RELEASE"
        classpath 'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:1.2.0'
    }
}

apply plugin: "io.spring.dependency-management"
apply plugin: 'org.unbroken-dome.test-sets'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'


sourceCompatibility = 1.8

dependencyManagement {
    imports {
        mavenBom 'org.arquillian:arquillian-universe:1.0.0.Alpha1'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.apache.openejb', name: 'arquillian-tomee-embedded', version: '1.7.2'
    testCompile group: 'junit', name: 'junit', version: '4.12'

    compileOnly group: 'javax', name: 'javaee-api', version: '7.0'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: hasProperty('ossrhUsername') ? ossrhUsername : System.getenv('ossrhUsername'),
                        password: hasProperty('ossrhPassword') ? ossrhPassword : System.getenv('ossrhPassword'))
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: hasProperty('ossrhUsername') ? ossrhUsername : System.getenv('ossrhUsername'),
                        password: hasProperty('ossrhPassword') ? ossrhPassword : System.getenv('ossrhPassword'))
            }

            pom.project {
                name 'BenGuard'
                packaging 'jar'
                artifactId jar.project.name.toLowerCase()
                description 'Fault tolerance library focused on Java EE'
                url 'https://github.com/Pscheidl/BenGuard'

                scm {
                    connection 'scm:git:git://github.com/Pscheidl/BenGuard.git'
                    developerConnection 'scm:git:ssh://github.com/Pscheidl/BenGuard.git'
                    url 'https://github.com/Pscheidl/BenGuard'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id 'pscheidl'
                        name 'Pavel Pscheidl'
                        email 'pavelpscheidl@gmail.com'
                    }
                }
            }
        }
    }
}

signing {
    sign configurations.archives
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}
